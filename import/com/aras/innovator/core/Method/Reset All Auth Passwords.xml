<AML>
 <Item type="Method" id="154591DE0A62401981BB5643A8650ABB" action="add">
  <execution_allowed_to keyed_name="Administrators" type="Identity">2618D6F5A90949BAA7E920D1B04C7EE1</execution_allowed_to>
  <method_code><![CDATA[' name: Reset All Auth Passwords
' purpose: reset the password property of all or selected users that are going
'          to be logging into innovator via the LOGON_USER client extension.
' created: 4-MAY-2005 George J. Carrette
' MethodTemplateName=VBMain;

Dim shared_secret As String
Dim allowed_domain_users_pat As System.Text.RegularExpressions.Regex
Dim denied_domain_users_pat As System.Text.RegularExpressions.Regex
Dim isShaHash As Boolean

Function setuserpassword(ByVal user_id As String, _
						 ByVal password As String) As String
	Dim sql As String = String.Format(CultureInfo.InvariantCulture, "UPDATE [USER] SET [PASSWORD] = '{0}' WHERE [ID] = '{1}'",
	CCO.DB.InnDatabase.QuoteValue(password), CCO.DB.InnDatabase.QuoteValue(user_id))
	CCO.DB.InnDatabase.ExecuteSQL(sql)
	Return "OK"
End Function

Friend Shared Function md5string16(ByVal input As String, ByVal forceSha As Boolean) As String
	Dim hashAlg As System.Security.Cryptography.HashAlgorithm = Nothing
	Try
		If Not forceSha Then
			Try
					hashAlg = new System.Security.Cryptography.MD5CryptoServiceProvider()
					Catch ex As InvalidOperationException
						'FIPS is ON in windows registry
						hashAlg = Nothing
				End Try
			End If
		If hashAlg Is Nothing Then
			hashAlg = new System.Security.Cryptography.SHA256CryptoServiceProvider()
		End If

		Dim asciiEncoding As New ASCIIEncoding
		Dim data As Byte() = asciiEncoding.GetBytes(input)
		Dim resulthash As Byte() = hashAlg.ComputeHash(data)
		Dim sb As StringBuilder = New StringBuilder(16)
		Dim j As Integer
		For j = 0 To resulthash.Length - 1
			sb.AppendFormat("{0:X2}", resulthash(j))
		Next
		Return sb.ToString()
		Finally
			if hashAlg IsNot Nothing Then
				CType(hashAlg, IDisposable).Dispose()
			End If
		End Try
End Function

Function resetuser(ByVal user_id As String, ByVal login_name As String) As String
	Dim result As String = CCO.Context.Server.HtmlEncode(login_name) + ": "
	If Not allowed_domain_users_pat.IsMatch(login_name) Then
		Return result + "did not match allowed_domain_users"
	End If
	If denied_domain_users_pat.IsMatch(login_name) Then
		Return result + "matched denied_domain_users"
	End If
	Dim password As String = md5string16(md5string16(shared_secret, isShaHash) + _
													md5string16(login_name.ToLower(), isShaHash) + _
													login_name.ToLower() + shared_secret, isShaHash)
	Return result + setuserpassword(user_id, password)
End Function

Function mainloop(ByVal user_id As String) As String
	If (Not String.IsNullOrEmpty(user_id)) Then
		user_id = user_id.Replace("'", "''")
	End If
	Dim sb As System.Text.StringBuilder = New System.Text.StringBuilder()
	Dim sql As String = "select id,login_name from [USER]"
	If (Not String.IsNullOrEmpty(user_id)) Then
		sql = sql + String.Format(CultureInfo.InvariantCulture, " where id='{0}'", CCO.DB.InnDatabase.QuoteValue(user_id))
	End If
	sql = sql + " order by login_name asc"
	Dim rs As Aras.Server.Core.InnovatorDataSet = CCO.DB.InnDatabase.ExecuteSelect(sql)
	Dim flag As Boolean = False
	While Not rs.Eof
		If flag Then
			sb.Append("<BR>")
		End If
		flag = True
		sb.Append(resetuser(CStr(rs.Value("id")), CStr(rs.Value("login_name"))))
		rs.MoveNext()
	End While
	Return sb.ToString()
End Function

Function main() As Item
	If Not CCO.Permissions.UserHasRootOrAdminIdentity() Then
		Return Me.getInnovator().newError("must be administrative user to reset passwords")
	End If
	Dim cfgpath As String = "/Innovator/ClientLogon"
	CCO.Startup.debuglog("Reset All Auth Passwords: IN", Me.dom)
	Dim cfg As XmlElement = _
	 CType(CCO.Cache.GetCacheInfo("ApplicationXML").SelectSingleNode(cfgpath), XmlElement)
	CCO.Startup.debuglog("Reset All Auth Passwords: CFG", cfg)
	Dim user_id_element As XmlElement = CType(Me.dom.SelectSingleNode("//id"), XmlElement)
	Dim user_id As String = ""
	If Not IsNothing(user_id_element) Then
		user_id = user_id_element.InnerText
	End If
	CCO.Startup.debuglog("Reset All Auth Passwords: ID", user_id)
	If IsNothing(cfg) Then
		Return Me.getInnovator().newError("cannot find " + cfgpath + " in configuration file")
	End If
	shared_secret = cfg.GetAttribute("shared_secret")
	allowed_domain_users_pat = _
	 New System.Text.RegularExpressions.Regex(cfg.GetAttribute("allowed_domain_users"), _
											 System.Text.RegularExpressions.RegexOptions.IgnoreCase)
	denied_domain_users_pat = _
	 New System.Text.RegularExpressions.Regex(cfg.GetAttribute("denied_domain_users"), _
											 System.Text.RegularExpressions.RegexOptions.IgnoreCase)
	isShaHash = String.Equals(cfg.GetAttribute("fipsMode"), "true", StringComparison.OrdinalIgnoreCase)
	Return Me.getInnovator().newResult(mainloop(user_id))
End Function]]></method_code>
  <method_type>VB</method_type>
  <name>Reset All Auth Passwords</name>
 </Item>
</AML>